IF(APPLE)
  FILE(GLOB_RECURSE tbb_dylibs libtbb*.dylib)
  FOREACH(l IN LISTS tbb_dylibs)
    IF(NOT IS_SYMLINK "${l}")
      FILE(INSTALL "${l}" DESTINATION "${CMAKE_INSTALL_PREFIX}/lib")
    ENDIF()
  ENDFOREACH()
ELSE()
  FILE(GLOB_RECURSE tbb_solibs libtbb*.so.*)
  SET(tbb_symlinkdir "${CMAKE_CURRENT_BINARY_DIR}/install_symlinks_tmp")
  FILE(MAKE_DIRECTORY "${tbb_symlinkdir}")
  FOREACH(tbb_solib IN LISTS tbb_solibs)
    IF(NOT IS_SYMLINK "${tbb_solib}")
      FILE(INSTALL "${tbb_solib}" DESTINATION "${CMAKE_INSTALL_PREFIX}/lib")
      GET_FILENAME_COMPONENT(tbb_solib_name "${tbb_solib}" NAME)
      STRING(REGEX REPLACE ".so.*$" ".so" tbb_solib_base "${tbb_solib_name}")
      SET(tbb_symlinkname "${tbb_symlinkdir}/${tbb_solib_base}")
      EXECUTE_PROCESS(COMMAND ${CMAKE_COMMAND} -E create_symlink
                              "${tbb_solib_name}" "${tbb_symlinkname}"
                      OUTPUT_QUIET ERROR_QUIET RESULT_VARIABLE tbb_symlink_ok)
      IF(tbb_symlink_ok EQUAL 0)
        FILE(INSTALL "${tbb_symlinkname}"
                     DESTINATION "${CMAKE_INSTALL_PREFIX}/lib")
      ELSE()
        MESSAGE(WARNING "Creating symlink for ${tbb_solib_base} -> ${tbb_solib_name} failed!")
      ENDIF()
      FILE(REMOVE "${tbb_symlinkname}")
    ENDIF()
  ENDFOREACH()
  FILE(REMOVE_RECURSE "${tbb_symlinkdir}")
ENDIF()
FILE(INSTALL include/tbb include/serial
     DESTINATION "${CMAKE_INSTALL_PREFIX}/include")
